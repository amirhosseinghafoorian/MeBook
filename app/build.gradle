plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 32

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.getProperty("keyPath"))
            storePassword properties.getProperty("keyPassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyAliasPassword")
        }
    }

    defaultConfig {
        applicationId "com.example.mebook"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    flavorDimensions "dev"
    productFlavors {
        mock {
            dimension "dev"
            applicationIdSuffix ".mock"
        }
        live {
            dimension "dev"
            applicationIdSuffix ".live"
        }
    }

    variantFilter { variant ->
        def name_types = variant.flavors*.name
        def name = name_types.join()

        if (variant.buildType.name == "release" && name.contains("mock")){
            setIgnore(true)
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "BASE_URL", "\"http://192.168.1.5:5000/\"")
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BASE_URL", "\"http://45.139.10.139:5000/\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.2"
    testImplementation("org.amshove.kluent:kluent:1.68")

    // coroutine for testing
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'

    // arch
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // mock
    testImplementation "io.mockk:mockk:1.12.5"

    // navigation
    implementation "androidx.navigation:navigation-compose:2.5.0"

    // accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.24.11-rc"

    // view model
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"

    // hilt
    implementation "com.google.dagger:hilt-android:2.43"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:2.43"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.3"

    // Room
    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    // data store
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // lottie
    implementation "com.airbnb.android:lottie-compose:5.2.0"
}